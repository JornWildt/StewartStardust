>> hvad_hvem_er (emne, erJaNej)
>> hvad_hvem_er (emne)
>> hvad_mener_du_om (emne)
>> kan_du_lide (emne)
>> spiser_du (emne)
>> kendt_ord (emne)
# Lookup any RDF data available
! repeatable
! call RDF.Query("
SELECT ?navn ?baggrund ?kommentar ?typeNavn ?typeBeskriver ?typeKommentar ?topic
WHERE
{
  ?subj zp:knownby ?emne.
  ?subj rdfs:label ?navn.
  OPTIONAL
  {
    ?mening zp:emne ?subj.
    OPTIONAL
    {
      ?mening zp:kommentar ?kommentar.
    }
    OPTIONAL
    {
      ?mening zp:baggrund ?baggrund.
    }
  }
  OPTIONAL { ?subj rdf:type ?type. }
  OPTIONAL { ?type rdfs:label ?typeNavn}
  OPTIONAL { ?type zp:element ?typeBeskriver }
  OPTIONAL 
  {
    ?typeMening zp:emne ?type.
    ?typeMening zp:kommentar ?typeKommentar.
  }
  OPTIONAL { ?subj zp:topic ?topic }
  FILTER (?emne = lcase(@emne))
}
ORDER BY RAND()
LIMIT 1
")
# If the result has a topic associated to it, then try to set that as the current chat topic
! call RDF.TrySetTopic($_, "topic")
: <VisRDFOplysning(erJaNej, first(result).navn, first(result).baggrund, first(result).kommentar, first(result).typeNavn, first(result).typeBeskriver, first(result).typeKommentar)>
{empty}: Hvad fanden ved jeg om <emne>!
#{empty}: Aldrig hørt om <emne>. 


# Kan ikke identificere "emne"
>> hvad_hvem_er
>> hvad_mener_du_om
>> kan_du_lide
>> kendt_ord
! repeatable
: Tror du jeg ved det måske?
: Sig mig ligner jeg et leksikon? Eller ved du heller ikke hvad det er - hva?


### HVILKE KENDER DU ####

>> hvilke_kender_du (emne)
! repeatable
! call RDF.Query("
SELECT ?name
WHERE
{
  ?subj rdf:type ?type.
  ?subj rdfs:label ?name.
  ?type rdf:type rdfs:Class.
  ?type zp:knownby ?emne
  FILTER (?emne = lcase(@emne))
}
ORDER BY RAND()
LIMIT 5
")
:Det kunne sgu da godt være <result:{r | <r.name>};separator=", ">
{empty}: Ikke en daddel mand
{empty}: Ska du værre fræk?

>> hvilke_kender_du
! repeatable
: Det er sgu' da for åndsvagt.
: Skal vi ikke hellere tage en bajer.


#### HAR DU BESØGT ####


>> har_du_besøgt (emne)
# Lookup any RDF data available
! repeatable
! call RDF.Query("
SELECT ?navn ?oplevelse ?kommentar ?topic
WHERE
{
  ?subj zp:knownby ?emne.
  ?subj rdfs:label ?navn.
  OPTIONAL
  {
    ?mening zp:emne ?subj.
    OPTIONAL { ?mening zp:kommentar ?kommentar. }
    OPTIONAL { ?mening zp:oplevelse ?oplevelse. }
  }
  OPTIONAL { ?subj zp:topic ?topic }
  FILTER (?emne = lcase(@emne))
}
ORDER BY RAND()
LIMIT 1
")
# If the result has a topic associated to it, then try to set that as the current chat topic
! call RDF.TrySetTopic($_, "topic")
: <VisRDFBesogt(first(result).navn, first(result).oplevelse, first(result).kommentar)>
{empty}: Næh!
#{empty}: Aldrig hørt om <emne>. 


# Kan ikke identificere "emne"
>> har_du_besøgt
! repeatable
: Nej. Men ...

#### HVAD XXX YYY ####

# hvad spiser mus
>> spm_attr_subj (attr:verb, subj:subst)
>> spm_attr_subj (attr:adj, subj:subst)
>> spm_attr_subj (attr:subst, subj:subst)
! repeatable
! call RDF.Single("
SELECT ?subj ?svar ?attr ?egenskab ?svar_tekst ?svar_simpel ?topic
WHERE
{
  ?subj zp:knownby ?subj_txt.
  ?attr zp:knownby ?attr_txt.
  OPTIONAL {
    ?attr zp:egenskab ?egenskab.
    ?subj ?egenskab ?subj_egenskab.
    ?subj_egenskab rdfs:label ?svar
  }
  OPTIONAL {
    ?attr zp:egenskab_simpel ?egenskab_simpel.
    ?subj ?egenskab_simpel ?svar_simpel.
  }
  OPTIONAL {
    ?attr zp:egenskab_tekst ?egenskab_tekst.
    ?subj ?egenskab_tekst ?svar_tekst.
  }
  OPTIONAL {?subj zp:topic ?topic}
  FILTER (?subj_txt = lcase(@subj))
  FILTER (?attr_txt = lcase(@attr))
}
ORDER BY RAND()
LIMIT 1
")
# If the result has a topic associated to it, then try to set that as the current chat topic
! call RDF.TrySetTopic($_, "topic")
: <if(svar_tekst)><svar_tekst><elseif(svar_simpel)><svar_simpel; format="UF"><elseif(svar)><svar; format="UF"> vil jeg mene?<else>Hmmm...<endif>
: <if(svar_tekst)><svar_tekst><elseif(svar_simpel)><svar_simpel; format="UF"><elseif(svar)><svar; format="UF"><else>Tjah?<endif>
: <if(svar_tekst)><svar_tekst><elseif(svar_simpel)><svar_simpel; format="UF"><elseif(svar)>Skulle det mon være <svar>?<else>øhm...<endif>
{empty}: Fårk man - nu sejler det sgu da.

>> { intent = spm_attr_subj }
! repeatable
: øh ...
: hvad?
